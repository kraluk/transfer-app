buildscript {
    dependencies {
        // fixes problems with missing JAX-B module in Java 11, used mainly in jooq.gradle
        // more details: https://github.com/jOOQ/jOOQ/issues/6879#issuecomment-439253436
        classpath group: "org.glassfish.jaxb", name: "jaxb-runtime", version: "2.3.2"
    }
}

plugins {
    id "application"
    id "java"
    id "groovy"
    id "idea"

    id "io.ratpack.ratpack-java" version "1.6.0"
    id "nu.studer.jooq" version "3.0.2"
    id "com.github.johnrengelman.shadow" version "4.0.4"
}

apply from: "jooq.gradle"

description = "TransferApp - Core"

group "io.kraluk.transferapp"
version "1.0.0-SNAPSHOT"

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: "io.ratpack", name: "ratpack-core", version: "$ratpackVersion"
    compile group: "io.ratpack", name: "ratpack-guice", version: "$ratpackVersion"
    compile group: "io.ratpack", name: "ratpack-hikari", version: "$ratpackVersion"
    compile group: "io.ratpack", name: "ratpack-h2", version: "$ratpackVersion"
    compile group: "io.ratpack", name: "ratpack-jdbc-tx", version: "$ratpackVersion"
    compile group: "io.ratpack", name: "ratpack-dropwizard-metrics", version: "$ratpackVersion"

    compile group: "com.h2database", name: "h2", version: "1.4.197"
    compile group: "org.jooq", name: "jooq", version: "$jooqVersion"

    runtime group: "org.slf4j", name: "slf4j-api", version: "1.7.25"
    runtime group: "ch.qos.logback", name: "logback-classic", version: "1.2.3"
    runtime group: "ch.qos.logback", name: "logback-core", version: "1.2.3"

    testCompile group: "io.ratpack", name: "ratpack-test", version: "$ratpackVersion"
    testCompile group: "io.ratpack", name: "ratpack-groovy-test", version: "$ratpackVersion"
    testCompile group: "org.codehaus.groovy", name: "groovy-all", version: "$groovyVersion"
    testCompile group: "org.spockframework", name: "spock-core", version: "$spockVersion"
    testCompile group: "org.hamcrest", name: "hamcrest-all", version: "1.3"
    testCompile group: "net.bytebuddy", name: "byte-buddy", version: "1.9.7"
    testCompile group: "org.objenesis", name: "objenesis", version: "3.0.1"

}

applicationDefaultJvmArgs = ["--add-opens", "java.base/java.lang=ALL-UNNAMED",
                             "--add-exports", "java.base/jdk.internal.misc=ALL-UNNAMED",
                             "-Dio.netty.tryReflectionSetAccessible=true",
                             "--illegal-access=warn"]

mainClassName = "io.kraluk.transferapp.TransferApp"

compileJava {
    options.incremental = true
    options.compilerArgs << '-parameters'
    options.compilerArgs << '-Xlint:unchecked'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs.add("-parameters")
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = "full"
    }
}
